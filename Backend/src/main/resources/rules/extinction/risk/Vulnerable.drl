package rules.extinction.risk;
dialect  "mvel"

import com.unep.wcmc.model.DensityData;
import com.unep.wcmc.model.DistributionArea;
import com.unep.wcmc.model.ExtinctionRiskCategory;
import com.unep.wcmc.model.ExtinctionRiskConfiguration;
import com.unep.wcmc.model.Habitat;
import com.unep.wcmc.model.NaturalHistory;
import com.unep.wcmc.model.PopulationDynamics;
import com.unep.wcmc.model.PopulationTrend;
import com.unep.wcmc.model.Species;
import com.unep.wcmc.model.TrendOccurence;
import com.unep.wcmc.model.Threat;

global Species species;
global java.util.List configuration;

/**
 * Vulnerable (VU) rules
 */
rule "Vulnerable (VU) - A1"
    activation-group "ExtinctionRisk"
    salience 0
    when
        $config : ExtinctionRiskConfiguration(category == ExtinctionRiskCategory.VUNERABLE) from configuration
        // criteria
        PopulationTrend(declineReversibleAndCeased == null)
    then
        species.setExtinctionRiskCategory(ExtinctionRiskCategory.VUNERABLE);
end

rule "Vulnerable (VU) - A2"
    activation-group "ExtinctionRisk"
    salience 0
    when
        $config : ExtinctionRiskConfiguration(category == ExtinctionRiskCategory.VUNERABLE) from configuration
        // criteria
        PopulationTrend(declineReversibleAndCeased == false)
    then
        species.setExtinctionRiskCategory(ExtinctionRiskCategory.VUNERABLE);
end

rule "Vulnerable (VU) - B1"
    activation-group "ExtinctionRisk"
    salience 0
    when
        $config : ExtinctionRiskConfiguration(category == ExtinctionRiskCategory.VUNERABLE) from configuration
        // criteria
        // B1,B2 (EOD or AOD)
        DistributionArea(extendOccurrence <= 20000, extendOccurrence > 5000) or
        DistributionArea(occupancyArea <= 2000, occupancyArea > 500)
        // others
        DistributionArea(trendExtendOccurence == TrendOccurence.DECLINING)
        (
            DistributionArea(trendOccupancyArea == TrendOccurence.DECLINING) or
            Habitat(continuingDeclineInHabitatQuality == true) or
            PopulationDynamics(matureIndividualsNumberTrend == TrendOccurence.DECLINING) or
            PopulationDynamics(extremeFlutuationInMatureIndividualsNumber == true) or
            DensityData(subPopulationsNumberTrend == TrendOccurence.DECLINING) or
            PopulationDynamics(populationSeverelyFragmented == true) or
            Threat(geographicallyDistinctAreasNumber <= 10)
        )
    then
        species.setExtinctionRiskCategory(ExtinctionRiskCategory.VUNERABLE);
end

rule "Vulnerable (VU) - B2"
    activation-group "ExtinctionRisk"
    salience 0
    when
        $config : ExtinctionRiskConfiguration(category == ExtinctionRiskCategory.VUNERABLE) from configuration
        // criteria
        // B1,B2 (EOD or AOD)
        DistributionArea(extendOccurrence <= 20000, extendOccurrence > 5000) or
        DistributionArea(occupancyArea <= 2000, occupancyArea > 500)
        // others
        DistributionArea(trendOccupancyArea == TrendOccurence.DECLINING)
        (
            DistributionArea(trendExtendOccurence == TrendOccurence.DECLINING) or
            Habitat(continuingDeclineInHabitatQuality == true) or
            PopulationDynamics(matureIndividualsNumberTrend == TrendOccurence.DECLINING) or
            PopulationDynamics(extremeFlutuationInMatureIndividualsNumber == true) or
            DensityData(subPopulationsNumberTrend == TrendOccurence.DECLINING) or
            PopulationDynamics(populationSeverelyFragmented == true) or
            Threat(geographicallyDistinctAreasNumber <= 10)
        )
    then
        species.setExtinctionRiskCategory(ExtinctionRiskCategory.VUNERABLE);
end

rule "Vulnerable (VU) - B3"
    activation-group "ExtinctionRisk"
    salience 0
    when
        $config : ExtinctionRiskConfiguration(category == ExtinctionRiskCategory.VUNERABLE) from configuration
        // criteria
        // B1,B2 (EOD or AOD)
        DistributionArea(extendOccurrence <= 20000, extendOccurrence > 5000) or
        DistributionArea(occupancyArea <= 2000, occupancyArea > 500)
        // others
        Habitat(continuingDeclineInHabitatQuality == true)
        (
            DistributionArea(trendExtendOccurence == TrendOccurence.DECLINING) or
            DistributionArea(trendOccupancyArea == TrendOccurence.DECLINING) or
            PopulationDynamics(matureIndividualsNumberTrend == TrendOccurence.DECLINING) or
            PopulationDynamics(extremeFlutuationInMatureIndividualsNumber == true) or
            DensityData(subPopulationsNumberTrend == TrendOccurence.DECLINING) or
            PopulationDynamics(populationSeverelyFragmented == true) or
            Threat(geographicallyDistinctAreasNumber <= 10)
        )
    then
        species.setExtinctionRiskCategory(ExtinctionRiskCategory.VUNERABLE);
end

rule "Vulnerable (VU) - B4"
    activation-group "ExtinctionRisk"
    salience 0
    when
        $config : ExtinctionRiskConfiguration(category == ExtinctionRiskCategory.VUNERABLE) from configuration
        // criteria
        // B1,B2 (EOD or AOD)
        DistributionArea(extendOccurrence <= 20000, extendOccurrence > 5000) or
        DistributionArea(occupancyArea <= 2000, occupancyArea > 500)
        // others
        PopulationDynamics(matureIndividualsNumberTrend == TrendOccurence.DECLINING)
        (
            DistributionArea(trendExtendOccurence == TrendOccurence.DECLINING) or
            DistributionArea(trendOccupancyArea == TrendOccurence.DECLINING) or
            Habitat(continuingDeclineInHabitatQuality == true) or
            PopulationDynamics(extremeFlutuationInMatureIndividualsNumber == true) or
            DensityData(subPopulationsNumberTrend == TrendOccurence.DECLINING) or
            PopulationDynamics(populationSeverelyFragmented == true) or
            Threat(geographicallyDistinctAreasNumber <= 10)
        )
    then
        species.setExtinctionRiskCategory(ExtinctionRiskCategory.VUNERABLE);
end

rule "Vulnerable (VU) - B5"
    activation-group "ExtinctionRisk"
    salience 0
    when
        $config : ExtinctionRiskConfiguration(category == ExtinctionRiskCategory.VUNERABLE) from configuration
        // criteria
        // B1,B2 (EOD or AOD)
        DistributionArea(extendOccurrence <= 20000, extendOccurrence > 5000) or
        DistributionArea(occupancyArea <= 2000, occupancyArea > 500)
        // others
        PopulationDynamics(extremeFlutuationInMatureIndividualsNumber == true)
        (
            DistributionArea(trendExtendOccurence == TrendOccurence.DECLINING) or
            DistributionArea(trendOccupancyArea == TrendOccurence.DECLINING) or
            Habitat(continuingDeclineInHabitatQuality == true) or
            PopulationDynamics(matureIndividualsNumberTrend == TrendOccurence.DECLINING) or
            DensityData(subPopulationsNumberTrend == TrendOccurence.DECLINING) or
            PopulationDynamics(populationSeverelyFragmented == true) or
            Threat(geographicallyDistinctAreasNumber <= 10)
        )
    then
        species.setExtinctionRiskCategory(ExtinctionRiskCategory.VUNERABLE);
end

rule "Vulnerable (VU) - B6"
    activation-group "ExtinctionRisk"
    salience 0
    when
        $config : ExtinctionRiskConfiguration(category == ExtinctionRiskCategory.VUNERABLE) from configuration
        // criteria
        // B1,B2 (EOD or AOD)
        DistributionArea(extendOccurrence <= 20000, extendOccurrence > 5000) or
        DistributionArea(occupancyArea <= 2000, occupancyArea > 500)
        // others
        DensityData(subPopulationsNumberTrend == TrendOccurence.DECLINING)
        (
            DistributionArea(trendExtendOccurence == TrendOccurence.DECLINING) or
            DistributionArea(trendOccupancyArea == TrendOccurence.DECLINING) or
            Habitat(continuingDeclineInHabitatQuality == true) or
            PopulationDynamics(matureIndividualsNumberTrend == TrendOccurence.DECLINING) or
            PopulationDynamics(extremeFlutuationInMatureIndividualsNumber == true) or
            PopulationDynamics(populationSeverelyFragmented == true) or
            Threat(geographicallyDistinctAreasNumber <= 10)
        )
    then
        species.setExtinctionRiskCategory(ExtinctionRiskCategory.VUNERABLE);
end

rule "Vulnerable (VU) - B7"
    activation-group "ExtinctionRisk"
    salience 0
    when
        $config : ExtinctionRiskConfiguration(category == ExtinctionRiskCategory.VUNERABLE) from configuration
        // criteria
        // B1,B2 (EOD or AOD)
        DistributionArea(extendOccurrence <= 20000, extendOccurrence > 5000) or
        DistributionArea(occupancyArea <= 2000, occupancyArea > 500)
        // others
        PopulationDynamics(populationSeverelyFragmented == true)
        (
            DistributionArea(trendExtendOccurence == TrendOccurence.DECLINING) or
            DistributionArea(trendOccupancyArea == TrendOccurence.DECLINING) or
            Habitat(continuingDeclineInHabitatQuality == true) or
            PopulationDynamics(matureIndividualsNumberTrend == TrendOccurence.DECLINING) or
            PopulationDynamics(extremeFlutuationInMatureIndividualsNumber == true) or
            DensityData(subPopulationsNumberTrend == TrendOccurence.DECLINING) or
            Threat(geographicallyDistinctAreasNumber <= 10)
        )
    then
        species.setExtinctionRiskCategory(ExtinctionRiskCategory.VUNERABLE);
end

rule "Vulnerable (VU) - B8"
    activation-group "ExtinctionRisk"
    salience 0
    when
        $config : ExtinctionRiskConfiguration(category == ExtinctionRiskCategory.VUNERABLE) from configuration
        // criteria
        // B1,B2 (EOD or AOD)
        DistributionArea(extendOccurrence <= 20000, extendOccurrence > 5000) or
        DistributionArea(occupancyArea <= 2000, occupancyArea > 500)
        // others
        Threat(geographicallyDistinctAreasNumber <= 10)
        (
            DistributionArea(trendExtendOccurence == TrendOccurence.DECLINING) or
            DistributionArea(trendOccupancyArea == TrendOccurence.DECLINING) or
            Habitat(continuingDeclineInHabitatQuality == true) or
            PopulationDynamics(matureIndividualsNumberTrend == TrendOccurence.DECLINING) or
            PopulationDynamics(extremeFlutuationInMatureIndividualsNumber == true) or
            DensityData(subPopulationsNumberTrend == TrendOccurence.DECLINING) or
            PopulationDynamics(populationSeverelyFragmented == true)
        )
    then
        species.setExtinctionRiskCategory(ExtinctionRiskCategory.VUNERABLE);
end

rule "Vulnerable (VU) - C"
    activation-group "ExtinctionRisk"
    salience 0
    when
        $config : ExtinctionRiskConfiguration(category == ExtinctionRiskCategory.VUNERABLE) from configuration
        // criteria
        PopulationDynamics(matureIndividualsNumber <=  $config.numberMatureIndividuals)
        (
            PopulationDynamics(declinePeriodPercent >= 10) or
            PopulationDynamics(matureIndividualsSubpopulationMaxNumber <= 1000) or
            PopulationDynamics(imatureIndividualsSubpopulationPercent >= 100) or
            PopulationDynamics(extremeFlutuationInMatureIndividualsNumber == true)
        )
    then
        species.setExtinctionRiskCategory(ExtinctionRiskCategory.VUNERABLE);
end

rule "Vulnerable (VU) - D"
    activation-group "ExtinctionRisk"
    salience 0
    when
        $config : ExtinctionRiskConfiguration(category == ExtinctionRiskCategory.VUNERABLE) from configuration
        // criteria
        PopulationDynamics(matureIndividualsNumber <=  $config.numberMatureIndividuals2)
    then
        species.setExtinctionRiskCategory(ExtinctionRiskCategory.VUNERABLE);
end


rule "Vulnerable (VU) - E"
    activation-group "ExtinctionRisk"
    salience 0
    when
        $config : ExtinctionRiskConfiguration(category == ExtinctionRiskCategory.VUNERABLE) from configuration
        // criteria
        PopulationTrend(brazilExtinctionProbability >= $config.probExtinctionInWild)
    then
        species.setExtinctionRiskCategory(ExtinctionRiskCategory.VUNERABLE);
end