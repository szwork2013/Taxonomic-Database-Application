package rules.extinction.risk;
dialect  "mvel"

import com.unep.wcmc.model.DistributionArea;
import com.unep.wcmc.model.ExtinctionRiskCategory;
import com.unep.wcmc.model.ExtinctionRiskConfiguration;
import com.unep.wcmc.model.NaturalHistory;
import com.unep.wcmc.model.PopulationDynamics;
import com.unep.wcmc.model.PopulationTrend;
import com.unep.wcmc.model.Species;
import com.unep.wcmc.model.TrendOccurence;
import com.unep.wcmc.model.Threat;

global Species species;
global java.util.List configuration;

rule "Critically Endangered (CR) - A"
    activation-group "ExtinctionRisk"
    salience 0
    when
        $config : ExtinctionRiskConfiguration(category == ExtinctionRiskCategory.CRITICALLY_ENDANGERED) from configuration
        // A1, A2, A3, A4 criteria
        PopulationTrend(declineReversibleAndCeased == false)
        PopulationTrend(percPopulationDecline >= $config.percPopulationDecline) // A1
        //PopulationTrend(percPopulationDecline >= $config.percePopulationDecline2) // A2, A3, A4
    then
        species.setExtinctionRiskCategory(ExtinctionRiskCategory.CRITICALLY_ENDANGERED);
end

rule "Critically Endangered (CR) - B"
    activation-group "ExtinctionRisk"
    salience 0
    when
        $config : ExtinctionRiskConfiguration(category == ExtinctionRiskCategory.CRITICALLY_ENDANGERED) from configuration
        // B1, B2 criteria
        DistributionArea(extendOccurrence < $config.occurrenceExtent) // B1 (EOD)
        DistributionArea(occupancyArea < $config.occupancyArea) // B2 (AOD)
    and (
        // B(a) criteria
        PopulationDynamics(populationSeverelyFragmented == true)
        or (
            // B(a) criteria
            PopulationDynamics(populationSeverelyFragmented == false)
            and
            Threat(geographicallyDistinctAreasNumber == $config.numberLocations)
        )
    )
    then
        species.setExtinctionRiskCategory(ExtinctionRiskCategory.CRITICALLY_ENDANGERED);
end

rule "Critically Endangered (CR) - C"
    activation-group "ExtinctionRisk"
    salience 0
    when
        $config : ExtinctionRiskConfiguration(category == ExtinctionRiskCategory.CRITICALLY_ENDANGERED) from configuration
        // C criteria
        PopulationDynamics(matureIndividualsNumber <  $config.numberMatureIndividuals)
    and (
        PopulationDynamics(declinePeriodPercent >= $config.percPopulationDecline) // C1
        or
        PopulationDynamics(matureIndividualsNumberTrend == TrendOccurence.DECLINING) // C2
        and (
                PopulationDynamics(matureIndividualsSubpopulationMaxNumber <= $config.numberMatureIndividualsInSub) // a(i)
                or
                PopulationDynamics(imatureIndividualsSubpopulationPercent >= 90)  // a(ii)
                or
                PopulationDynamics(extremeFlutuationInMatureIndividualsNumber == true) // b
            )
        )
    then
        species.setExtinctionRiskCategory(ExtinctionRiskCategory.CRITICALLY_ENDANGERED);
end

rule "Critically Endangered (CR) - D"
    activation-group "ExtinctionRisk"
    salience 0
    when
        $config : ExtinctionRiskConfiguration(category == ExtinctionRiskCategory.CRITICALLY_ENDANGERED) from configuration
        // D criteria
        PopulationDynamics(matureIndividualsNumber < $config.numberMatureIndividuals2)
    then
        species.setExtinctionRiskCategory(ExtinctionRiskCategory.CRITICALLY_ENDANGERED);
end

rule "Critically Endangered (CR) - E"
    activation-group "ExtinctionRisk"
    salience 0
    when
        $config : ExtinctionRiskConfiguration(category == ExtinctionRiskCategory.CRITICALLY_ENDANGERED) from configuration
        // E criteria
        PopulationTrend(brazilExtinctionProbability >= $config.probExtinctionInWild, brazilExtinctionProbability < 20)
    then
        species.setExtinctionRiskCategory(ExtinctionRiskCategory.CRITICALLY_ENDANGERED);
end