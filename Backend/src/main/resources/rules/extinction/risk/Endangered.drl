package rules.extinction.risk;
dialect  "mvel"

import com.unep.wcmc.model.DistributionArea;
import com.unep.wcmc.model.ExtinctionRiskCategory;
import com.unep.wcmc.model.ExtinctionRiskConfiguration;
import com.unep.wcmc.model.NaturalHistory;
import com.unep.wcmc.model.PopulationDynamics;
import com.unep.wcmc.model.PopulationTrend;
import com.unep.wcmc.model.Species;
import com.unep.wcmc.model.TrendOccurence;
import com.unep.wcmc.model.Threat;

global Species species;
global java.util.List configuration;

/**
 * Endangered (EN)
 */
rule "Endangered (EN) - A"
    activation-group "ExtinctionRisk"
    salience 0
    when
        $config : ExtinctionRiskConfiguration(category == ExtinctionRiskCategory.ENDANGERED) from configuration
        // A1, A2, A3, A4 criteria
        PopulationTrend(declineReversibleAndCeased == false)
        PopulationTrend(percPopulationDecline >= $config.percPopulationDecline, percPopulationDecline < 90) // A1
    or
        PopulationTrend(percPopulationDecline >= $config.percePopulationDecline2, percPopulationDecline < 80) // A2, A3, A4
    then
        species.setExtinctionRiskCategory(ExtinctionRiskCategory.ENDANGERED);
end

rule "Endangered (EN) - B"
    activation-group "ExtinctionRisk"
    salience 0
    when
        $config : ExtinctionRiskConfiguration(category == ExtinctionRiskCategory.ENDANGERED) from configuration
        // B1, B2 criteria
        DistributionArea(extendOccurrence >= 100, extendOccurrence < $config.occurrenceExtent) // B1 (EOD)
        DistributionArea(occupancyArea >= 10, occupancyArea < $config.occupancyArea) // B2 (AOD)
    and (
        // B(a) criteria
        PopulationDynamics(populationSeverelyFragmented == true)
        or (
            // B(a) criteria
            PopulationDynamics(populationSeverelyFragmented == false)
            and
            Threat(geographicallyDistinctAreasNumber > 1, geographicallyDistinctAreasNumber <= $config.numberLocations)
        )
    )
    then
        species.setExtinctionRiskCategory(ExtinctionRiskCategory.ENDANGERED);
end

rule "Endangered (EN) - C"
    activation-group "ExtinctionRisk"
    salience 0
    when
        $config : ExtinctionRiskConfiguration(category == ExtinctionRiskCategory.ENDANGERED) from configuration
        // C criteria
        PopulationDynamics(matureIndividualsNumber >= 250, matureIndividualsNumber < $config.numberMatureIndividuals)
    and (
        PopulationDynamics(declinePeriodPercent >= 20) // C1
        or
        PopulationDynamics(matureIndividualsNumberTrend == TrendOccurence.DECLINING) // C2
        and (
                PopulationDynamics(matureIndividualsSubpopulationMaxNumber <= 250) // a(i)
                or
                PopulationDynamics(imatureIndividualsSubpopulationPercent >= 95)  // a(ii)
                or
                PopulationDynamics(extremeFlutuationInMatureIndividualsNumber == true) // b
            )
        )
    then
        species.setExtinctionRiskCategory(ExtinctionRiskCategory.ENDANGERED);
end


rule "Endangered (EN) - D"
    activation-group "ExtinctionRisk"
    salience 0
    when
        $config : ExtinctionRiskConfiguration(category == ExtinctionRiskCategory.ENDANGERED) from configuration
        // D criteria
        PopulationDynamics(matureIndividualsNumber < $config.numberMatureIndividuals2)
    then
        species.setExtinctionRiskCategory(ExtinctionRiskCategory.ENDANGERED);
end

rule "Endangered (EN) - E"
    activation-group "ExtinctionRisk"
    salience 0
    when
        // E criteria
        PopulationTrend(brazilExtinctionProbability >= 20, brazilExtinctionProbability < 50)
    then
        species.setExtinctionRiskCategory(ExtinctionRiskCategory.ENDANGERED);
end
