package rules.extinction.risk;
dialect  "mvel"

import com.unep.wcmc.model.DensityData;
import com.unep.wcmc.model.DistributionArea;
import com.unep.wcmc.model.ExtinctionRiskCategory;
import com.unep.wcmc.model.ExtinctionRiskConfiguration;
import com.unep.wcmc.model.Habitat;
import com.unep.wcmc.model.NaturalHistory;
import com.unep.wcmc.model.PopulationDynamics;
import com.unep.wcmc.model.PopulationTrend;
import com.unep.wcmc.model.Species;
import com.unep.wcmc.model.TrendOccurence;
import com.unep.wcmc.model.Threat;

global Species species;
global java.util.List configuration;

/**
 * Endangered (EN) rules
 */
rule "Endangered (EN) - A1"
    activation-group "ExtinctionRisk"
    salience 0
    when
        $config : ExtinctionRiskConfiguration(category == ExtinctionRiskCategory.ENDANGERED) from configuration
        // criteria
        PopulationTrend(percPopulationDecline >= $config.percPopulationDecline, percPopulationDecline < 90) // A1
        PopulationTrend(declineReversibleAndCeased == null)
    then
        species.setExtinctionRiskCategory(ExtinctionRiskCategory.ENDANGERED);
end

rule "Endangered (EN) - A2"
    activation-group "ExtinctionRisk"
    salience 0
    when
        $config : ExtinctionRiskConfiguration(category == ExtinctionRiskCategory.ENDANGERED) from configuration
        // criteria
        PopulationTrend(percPopulationDecline >= $config.percePopulationDecline2, percPopulationDecline < 80) // A2, A3, A4
        PopulationTrend(declineReversibleAndCeased == false)
    then
        species.setExtinctionRiskCategory(ExtinctionRiskCategory.ENDANGERED);
end

rule "Endangered (EN) - B"
    activation-group "ExtinctionRisk"
    salience 0
    when
        $config : ExtinctionRiskConfiguration(category == ExtinctionRiskCategory.ENDANGERED) from configuration
        // criteria
        // B1,B2 (EOD or AOD)
        DistributionArea(extendOccurrence <= 5000, extendOccurrence > 100) or
        DistributionArea(occupancyArea <= 500, occupancyArea > 10)
        // others
        (
            DistributionArea(trendExtendOccurence == TrendOccurence.DECLINING) or
            DistributionArea(trendOccupancyArea == TrendOccurence.DECLINING) or
            Habitat(continuingDeclineInHabitatQuality == true) or
            PopulationDynamics(matureIndividualsNumberTrend == TrendOccurence.DECLINING) or
            PopulationDynamics(extremeFlutuationInMatureIndividualsNumber == true) or
            DensityData(subPopulationsNumberTrend == TrendOccurence.DECLINING) or
            PopulationDynamics(populationSeverelyFragmented == true) or
            Threat(geographicallyDistinctAreasNumber <= 5)
        )
    and
        (
            DistributionArea(trendExtendOccurence == TrendOccurence.DECLINING) or
            DistributionArea(trendOccupancyArea == TrendOccurence.DECLINING) or
            Habitat(continuingDeclineInHabitatQuality == true) or
            PopulationDynamics(matureIndividualsNumberTrend == TrendOccurence.DECLINING) or
            PopulationDynamics(extremeFlutuationInMatureIndividualsNumber == true) or
            DensityData(subPopulationsNumberTrend == TrendOccurence.DECLINING) or
            PopulationDynamics(populationSeverelyFragmented == true) or
            Threat(geographicallyDistinctAreasNumber <= 5)
        )
    then
        species.setExtinctionRiskCategory(ExtinctionRiskCategory.ENDANGERED);
end

rule "Endangered (EN) - C"
    activation-group "ExtinctionRisk"
    salience 0
    when
        $config : ExtinctionRiskConfiguration(category == ExtinctionRiskCategory.ENDANGERED) from configuration
        // criteria
        PopulationDynamics(matureIndividualsNumber <=  $config.numberMatureIndividuals)
        (
            PopulationDynamics(declinePeriodPercent >= 20) or
            PopulationDynamics(matureIndividualsSubpopulationMaxNumber <= 250) or
            PopulationDynamics(imatureIndividualsSubpopulationPercent >= 95) or
            PopulationDynamics(extremeFlutuationInMatureIndividualsNumber == true)
        )
    then
        species.setExtinctionRiskCategory(ExtinctionRiskCategory.ENDANGERED);
end

rule "Endangered (EN) - D"
    activation-group "ExtinctionRisk"
    salience 0
    when
        $config : ExtinctionRiskConfiguration(category == ExtinctionRiskCategory.ENDANGERED) from configuration
        // criteria
        PopulationDynamics(matureIndividualsNumber <=  $config.numberMatureIndividuals2)
    then
        species.setExtinctionRiskCategory(ExtinctionRiskCategory.ENDANGERED);
end


rule "Endangered (EN) - E"
    activation-group "ExtinctionRisk"
    salience 0
    when
        $config : ExtinctionRiskConfiguration(category == ExtinctionRiskCategory.ENDANGERED) from configuration
        // criteria
        PopulationTrend(brazilExtinctionProbability >= $config.probExtinctionInWild)
    then
        species.setExtinctionRiskCategory(ExtinctionRiskCategory.ENDANGERED);
end
