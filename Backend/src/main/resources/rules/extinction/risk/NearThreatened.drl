package rules.extinction.risk;
dialect  "mvel"

import com.unep.wcmc.model.Conservation;
import com.unep.wcmc.model.DistributionArea;
import com.unep.wcmc.model.ExtinctionRiskCategory;
import com.unep.wcmc.model.ExtinctionRiskConfiguration;
import com.unep.wcmc.model.PopulationDynamics;
import com.unep.wcmc.model.PopulationTrend;
import com.unep.wcmc.model.Specie
import com.unep.wcmc.model.Taxonomy;
import com.unep.wcmc.model.Threat;
import com.unep.wcmc.model.TrendOccurence;

global Specie specie;
global java.util.List configuration;

/**
 * Near Threatened (NT)
 * Classification - A
 */
rule "Near Threatened (NT) - A"
    activation-group "ExtinctionRisk"
    salience 10
    when
        $config : ExtinctionRiskConfiguration(category == ExtinctionRiskCategory.NEAR_THREATENED) from configuration
        // A1, A2, A3, A4 criteria
        Taxonomy(limitationsForAssessment == false)
        DistributionArea(onlyFromFewLocalities == true)

        PopulationTrend(declineReversibleAndCeased == false)
        PopulationTrend(percPopulationDecline > $config.percPopulationDecline, percPopulationDecline < 50) // A1
        PopulationTrend(percPopulationDecline > $config.percePopulationDecline2, percPopulationDecline < 40) // A2, A3, A4
    then
        specie.setExtinctionRiskCategory(ExtinctionRiskCategory.NEAR_THREATENED);
end

rule "Near Threatened (NT) - B"
    activation-group "ExtinctionRisk"
    salience 10
    when
        $config : ExtinctionRiskConfiguration(category == ExtinctionRiskCategory.NEAR_THREATENED) from configuration
        // B1, B2 criteria
        DistributionArea(extendOccurrence >= $config.occurrenceExtent, extendOccurrence < 30000) // B1 (EOD)
        DistributionArea(occupancyArea >= $config.occupancyArea, occupancyArea < 3000) // B2 (AOD)
    and (
        // B(a) criteria
        PopulationDynamics(populationSeverelyFragmented == true)
        or (
            // B(a) criteria
            PopulationDynamics(populationSeverelyFragmented == false)
            and
            Threat(geographicallyDistinctAreasNumber > 5, geographicallyDistinctAreasNumber <= 10)
        )
    )
    then
        specie.setExtinctionRiskCategory(ExtinctionRiskCategory.NEAR_THREATENED);
end

rule "Near Threatened (NT) - C"
    activation-group "ExtinctionRisk"
    salience 10
    when
        $config : ExtinctionRiskConfiguration(category == ExtinctionRiskCategory.NEAR_THREATENED) from configuration
        // C criteria
        PopulationDynamics(matureIndividualsNumber > 10000, matureIndividualsNumber < 15000)
    and (
            PopulationDynamics(declinePeriodPercent <= 10) // C1
            or (
                PopulationDynamics(matureIndividualsNumberTrend == TrendOccurence.DECLINING) // C2
                and
                PopulationDynamics(matureIndividualsSubpopulationMaxNumber > 10000,
                    matureIndividualsSubpopulationMaxNumber < 15000) // a(i)
            )
        )
    then
        specie.setExtinctionRiskCategory(ExtinctionRiskCategory.NEAR_THREATENED);
end


rule "Near Threatened (NT) - D"
    activation-group "ExtinctionRisk"
    salience 10
    when
        $config : ExtinctionRiskConfiguration(category == ExtinctionRiskCategory.NEAR_THREATENED) from configuration
        // D1 criteria
        PopulationDynamics(matureIndividualsNumber > 1000, matureIndividualsNumber < 1500) // D1
    then
        specie.setExtinctionRiskCategory(ExtinctionRiskCategory.NEAR_THREATENED);
end
